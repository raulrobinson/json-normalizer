plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(
                project.findProperty("javaVersion") as int
        )
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {

    // Jackson BOM
    api platform('com.fasterxml.jackson:jackson-bom:2.17.1')

    // Jackson Databind
    api 'com.fasterxml.jackson.core:jackson-databind'

    // (Opcional)
    api 'com.fasterxml.jackson.core:jackson-core'
    api 'com.fasterxml.jackson.core:jackson-annotations'

    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly  'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testRuntimeOnly  'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'json-normalizer'
            version = project.version
        }
    }
    repositories {
        mavenLocal()
    }
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {

    violationRules {

        rule {
            element = 'BUNDLE'

            limit {
                counter = 'INSTRUCTION'
                value   = 'COVEREDRATIO'
                minimum = 0.70   // 70% m√≠nimo
            }
        }

        rule {
            element = 'CLASS'

            excludes = [
                    '**/*Test*',
                    '**/JsonStringDetector*'
            ]

            limit {
                counter = 'LINE'
                value   = 'COVEREDRATIO'
                minimum = 0.60
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification